#!/usr/bin/perl -t
#
# Convert transcript into an Nvivo-friendly TSV form.
#
# Supported input formats:
#
#  - VTT generated by MS Teams
#  - text generated by MS Word
#
# Example: to transform a transcript with a 2s time shift:
#
#   $ xscript2nvivo.pl 2000 < input.vtt > output.tsv
#
# Copyright (c) 2022 Andrew Bower <andrew@bower.org.uk>
#
# SPDX-License-Identifier: BSD-2-Clause

use strict;
use Math::Round;

my $state = 'IDLE';
my $quantum = 0.1;
my @format = ( 'Timespan', 'Speaker', 'Content' );
my $tshift_ms = int(shift);

my $record;
my @records;

sub quantise {
  my ($h, $m, $s, $ms) = @_;
  my $time = int(int($ms) / 1000.0 / $quantum);
  $time += int($s) * 1.0 / $quantum;
  $time += int($m) * 60.0 / $quantum;
  $time += int($h) * 3600.0 / $quantum;
  $time += $tshift_ms / 1000.0 / $quantum;
  return $time;
}

while (<STDIN>) {
  chomp;
  if ($state eq 'IDLE') {
    if (/^WEBVTT/) {
      $state = 'VTT';
    } elsif (/\bAudio file\b/) {
      $state = 'MSWORD';
    }
  } elsif ($state eq 'VTT') {
    if (my @fields = /(\d+):(\d+):(\d+)\.(\d+) --> .*/) {
      $record = {};
      $record->{'time'} = quantise @fields;
      $state = 'TIME';
    }
  } elsif ($state eq 'TIME') {
    if (m,<v ([^>]*)>(.*)</v>,) {
      $record->{'Speaker'} = $1;
      $record->{'Content'} = $2;
      push @records, $record;
      $state = 'VTT';
    }
  } elsif ($state eq 'MSWORD') {
    if (/\bTranscript\b/) {
      $state = 'MSWORD-TRANSCRIPT';
    }
  } elsif ($state eq 'MSWORD-TRANSCRIPT') {
    if (my ($h, $m, $s, $speaker) = /^(\d+):(\d+):(\d+)\s+(.*)/) {
      $record = {};
      $record->{'time'} = quantise ($h, $m, $s);
      $record->{'Speaker'} = $speaker;
      $state = 'MSWORD-SPEAKER';
    }
  } elsif ($state eq 'MSWORD-SPEAKER') {
    $record->{'Content'} = $_;
    push @records, $record;
    $state = 'MSWORD-TRANSCRIPT';
  }
};

@records = sort { $a->{'time'} <=> $b->{'time'} } @records;

my $latest = -$quantum;

print join("\t", @format);
print "\n";

for my $record (@records) {
  my $t = $record->{'time'};
  if ($t <= $latest) {
    $t = $latest + 1;
  }

  my $secs = $t * $quantum;
  my $h = int($secs / 3600.0);
  $secs -= $h * 3600.0;
  my $m = int($secs / 60.0);
  $secs -= $m * 60.0;
  my $s = int($secs);
  $secs -= $s;
  my $ms = $secs * 1000;

  # Change formatting if quantum changes from 0.1
  $record->{'Timespan'} = sprintf("%02d:%02d:%02d.%.0f", $h, $m, $s, round ($ms / 1000.0 / $quantum));

  print join("\t", (map { $record->{$_} } @format));
  print "\n";

  $latest = $t;
}
